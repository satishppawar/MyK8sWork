# Day Seven - Pipeline as Code

---

In this section we will describe _Pipeline as Code_.  And specifically, we will discuss how it is implemented in _Jenkins_.

---


_Pipeline as Code_ is when the stages of an automation job, such as is used for CI/CD, are described in code (aka, a pipeline script).  Whereas, the traditional method for defining these stages would have been to use some form of GUI.  The pipeline script is usually implemented using a _Domain Specific Language_ (DSL) and is checked into the same source control repository as the code that it works with.


_Jenkins_ _Pipeline as Code_ is implemented using a `Jenkinsfile`.  There are two types of `Jenkinsfile`, an older scripted type and a newer declarative type.  As the name would suggest, the scripted `Jenkinsfile` uses scripts to define the stages.  The scripts are written in [_Groovy_](http://www.groovy-lang.org).  This is very powerful and allows for complex flow control and checks.  The newer declarative `Jenkinsfile` has a more controlled syntax, with the intention being to make it easier to create pipelines.  The great thing about having both supported is that when your use case is complex you can fall back on scripted `Jenkinsfiles` but use declarative `Jenkinsfiles` for everything else.

This [article](https://jenkins.io/blog/2017/02/03/declarative-pipeline-ga/) announces declarative pipeline.  And this [article](https://jenkins.io/doc/book/pipeline/getting-started/) describes how to get started with pipeline.

The type of `Jenkinsfile` that we will use for the `first-app` is the scripted type.  This is because we want to use the _Jenkins_ [_Kubernetes_ plugin](https://plugins.jenkins.io/kubernetes) in a way that is not supported using declarative pipeline.  When we installed _Jenkins_ into our cluster it was preconfigured with the _Kubernetes_ plugin.  One of the great things about _Jenkins_ is that it has literally hundred of plugins that solve many problems.  You can search for plugins on the [_Plugins Index_](https://plugins.jenkins.io).  

You can find many pipeline examples [here](https://github.com/jenkinsci/pipeline-examples).  And, _Jenkins Kubernetes_ plugin specific examples [here](https://github.com/jenkinsci/kubernetes-plugin/tree/master/examples).

So, let's have a look at the `Jenkinsfile` for the `first-app`.  It is called `Jenkinsfile.first-app` and lives in the root of the [_Kubernetes in 7 Days_](https://github.com/PacktPublishing/kubernetes-in-7-days-video) repository.

```groovy
def label = "mypod-${UUID.randomUUID().toString()}"
podTemplate(label: label, 
  serviceAccount: 'jenkins',
  containers: [
    containerTemplate(
      name: 'docker', 
      image: 'docker:17.12.1-ce',
      ttyEnabled: true, 
      command: 'cat',
      envVars: [
        envVar(key: 'DOCKER_HOST', value: 'tcp://dind:2375')
      ]
    ),
    containerTemplate(
      name: 'helm', 
      image: 'lachlanevenson/k8s-helm:v2.8.1', 
      ttyEnabled: true, 
      command: 'cat'
    )
  ],
  volumes: [
    emptyDirVolume(mountPath: '/var/lib/docker', memory: false)
  ]) {

    node(label) {
      
      // Checkout code    
      container('jnlp') {
        stage('Checkout code') {
          checkout scm
          env.commit = sh returnStdout: true, script: 'git rev-parse HEAD'
        }
      }

      // Build and push image
      container('docker') {
        stage('Build image') {
          env.version = sh returnStdout: true, script: 'cat build.number'            
          withEnv(['VERSION=' + env.version.trim(), 'COMMIT=' + env.commit.trim()]) {
            dir('first-app') {
              sh """
                docker build \
                  -t desdrury/first-app:${VERSION}.${COMMIT}  \
                  -t desdrury/first-app:latest \
                  .
              """
            }
          }
        }

        stage('Push image') {
          withDockerRegistry([credentialsId: 'docker-hub-user']) {
            withEnv(['VERSION=' + env.VERSION.trim(), 'COMMIT=' + env.COMMIT.trim()]) {
              sh "docker push desdrury/first-app:${VERSION}.${COMMIT}"
              sh 'docker push desdrury/first-app:latest'
            }
          }
        }
      }

      // Deploy Helm Chart
      container('helm') {
        stage('Deploy Helm Chart') {
          dir('charts') {
            withEnv(['VERSION=' + env.version.trim(), 'COMMIT=' + env.commit.trim()]) {
              sh """
                helm upgrade --install first-app \
                  --namespace production \
                  --set image.repository=desdrury/first-app \
                  --set image.tag=${VERSION}.${COMMIT} \
                  --set ingress.enabled=true \
                  --set ingress.hosts[0]=first-app-cicd.192.168.26.11.nip.io \
                  first-app
              """
            }
          }
        }
      }

    }
}
```

It might look a bit intimidating at first but really there is not too much going on here.  The first section of the file, beginning with `podTemplate`, is defining a dynamic build agent.  More on that in a moment.  The second section, beginning with `node(label)`, is where the stages of the pipeline are defined.

The great thing about using the _Jenkins Kubernetes_ plugin is that you can compose a build agent dynamically from _Docker_ containers.  Traditionally you would have to create a set of machines to act as the build agents for an automation server.  You would also need to install all of the tools necessary for the pipelines that would execute on those build agents.  And often this led to incompatible versions of tools being needed.  As well as an old build causing a newer one to fail if the working directory for the pipeline was not cleaned up properly.  Dynamic build agents are defined and created by a pipeline.  They run for as long as the pipeline and then get removed again once the pipeline has completed.  However, there are many other options that you can set to customise the default behaviour of the dyanmic build agents.

So, let's see how we define our dynamic build agent.

```groovy
def label = "mypod-${UUID.randomUUID().toString()}"
podTemplate(label: label, 
  serviceAccount: 'jenkins',
  containers: [
    containerTemplate(
      name: 'docker', 
      image: 'docker:17.12.1-ce',
      ttyEnabled: true, 
      command: 'cat',
      envVars: [
        envVar(key: 'DOCKER_HOST', value: 'tcp://dind:2375')
      ]
    ),
    containerTemplate(
      name: 'helm', 
      image: 'lachlanevenson/k8s-helm:v2.8.1', 
      ttyEnabled: true, 
      command: 'cat'
    )
  ],
  volumes: [
    emptyDirVolume(mountPath: '/var/lib/docker', memory: false)
  ]) {
...
...
```

Within the `containers` list inside the `podTemplate` function there are a number of `containerTemplate` definitions.  Each of these `containerTemplate` definitions describe a container that will be used in one or more stages of the pipeline.  In this particular pipeline there is a container for using the `docker` command and there is also a container for running `helm` commands.  Finally, there is also a hidden container called `jnlp` that is always created.  The `jnlp` container hosts the process that connects to _Jenkins_ and makes this _Pod_ into a build agent.

Now, let's look at the stages.

```groovy
...
...
    node(label) {

      // Checkout code    
      container('jnlp') {
        stage('Checkout code') {
          checkout scm
          env.commit = sh returnStdout: true, script: 'git rev-parse HEAD'
        }
      }

      // Build and push image
      container('docker') {
        stage('Build image') {
          env.version = sh returnStdout: true, script: 'cat build.number'            
          withEnv(['VERSION=' + env.version.trim(), 'COMMIT=' + env.commit.trim()]) {
            dir('first-app') {
              sh """
                docker build \
                  -t desdrury/first-app:${VERSION}.${COMMIT}  \
                  -t desdrury/first-app:latest \
                  .
              """
            }
          }
        }

        stage('Push image') {
          withDockerRegistry([credentialsId: 'docker-hub-user']) {
            withEnv(['VERSION=' + env.VERSION.trim(), 'COMMIT=' + env.COMMIT.trim()]) {
              sh "docker push desdrury/first-app:${VERSION}.${COMMIT}"
              sh 'docker push desdrury/first-app:latest'
            }
          }
        }
      }

      // Deploy Helm Chart
      container('helm') {
        stage('Deploy Helm Chart') {
          dir('charts') {
            withEnv(['VERSION=' + env.version.trim(), 'COMMIT=' + env.commit.trim()]) {
              sh """
                helm upgrade --install first-app \
                  --namespace production \
                  --set image.repository=desdrury/first-app \
                  --set image.tag=${VERSION}.${COMMIT} \
                  --set ingress.enabled=true \
                  --set ingress.hosts[0]=first-app-cicd.192.168.26.11.nip.io \
                  first-app
              """
            }
          }
        }
      }

    }
}
```

Each `stage` must run within the context of a `container`.  Each stage has a name that will be displayed in the _Jenkins_ web UI and also the console output.  We will see this when we run the `first-app` pipleline in the next section.  Within the `stage` are various DSL functions and _Groovy_ code blocks.  The most common DSL function in this example is the `sh` function that runs a Shell command in the container within which the `stage` is defined.

From looking at the stages in this example we can see that it is an extremely simple pipeline, only doing the following (as taken from the stage names).

* Checkout code
* Build image
* Push image
* Deploy Helm Chart

This is an incredibly brief introduction to pipelines in _Jenkins_.  The [_Jenkins Pipeline Book_](https://jenkins.io/doc/book/pipeline/) has extensive information about `Jenkinsfiles`.


What we have done in this section is to describe the concept of _Pipeline as Code_.  When then went on to explore how Jenkins implements _Pipeline as Code_ using `Jenkinsfiles`.  Finally, we had a look at the `Jenkinsfile` we will use for the `first-app` in the next section.


# Next

In the next section we will configure _Jenkins_ to run a pipeline that will build and deploy the `first-app`.

[Next](07-05.md)
